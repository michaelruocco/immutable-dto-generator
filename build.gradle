plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup:javapoet:1.10.0'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'junit:junit:4.12'
    compile 'org.assertj:assertj-core:3.9.1'
    compile 'org.mockito:mockito-all:1.10.19'
    compileOnly 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'org.slf4j:slf4j-log4j12:1.7.25'
    testCompile 'com.github.michaelruocco:file-loader:3.0.3'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}
signArchives.enabled = gradle.startParameter.taskNames.contains("uploadArchives")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'Immutable DTO Generator'
                description 'Code library to auto-generate code for immutable dto classes'
                groupId 'com.github.michaelruocco'
                artifactId 'immutable-dto-generator'
                version '1.0.0'
                url 'https://github.com/michaelruocco/immutable-dto-generator'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/immutable-dto-generator.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/immutable-dto-generator.git'
                    url 'git@github.com/michaelruocco/immutable-dto-generator.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}